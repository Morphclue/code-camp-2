plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

android {
    compileSdk 31

    defaultConfig {
        applicationId "org.feature.fox.coffee_counter"
        minSdk 26
        targetSdk 31
        versionCode 1
        versionName "1.0"

        buildConfigField "String", "BASE_URL", '"http://141.51.114.20:8080"'
        buildConfigField "String", "USERS_ENDPOINT", '"users"'
        buildConfigField "String", "ITEMS_ENDPOINT", '"items"'
        buildConfigField "String", "LOGIN_ENDPOINT", '"login"'
        buildConfigField "String", "DATE_PATTERN", '"dd.MM.yy"'
        buildConfigField "String", "LINE_CHART_PATTERN", '"dd.MMM"'
        buildConfigField "String", "REACH_SERVER_ERROR", '"Could not reach the server."'
        buildConfigField "String", "UNKNOWN_ERROR", '"Unknown error occurred."'
        buildConfigField "String", "USER_ID", '"USER_ID"'
        buildConfigField "String", "USER_PASSWORD", '"USER_PASSWORD"'
        buildConfigField "String", "REMEMBER_ME", '"REMEMBER_ME"'
        buildConfigField "String", "EXPIRATION", '"EXPIRATION"'
        buildConfigField "String", "BEARER_TOKEN", '"BEARER_TOKEN"'
        buildConfigField "String", "IS_ADMIN", '"IS_ADMIN"'
        buildConfigField "String", "NOTIFICATION_CHANNEL_ID", '"notification_channel"'

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }

        kapt {
            arguments {
                arg("room.schemaLocation", "$projectDir/schemas")
            }
        }
        hilt { enableAggregatingTask = true }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
        }

    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    namespace 'org.feature.fox.coffee_counter'

    kotlin.sourceSets.all {
        languageSettings.useExperimentalAnnotation("kotlin.RequiresOptIn")
    }
}

dependencies {
    def hilt_version = "2.38.1"
    def room_version = "2.4.2"
    def retrofit_version = "2.9.0"
    def moshi_version = "1.13.0"
    def kotlin_version = "1.6.2"
    def arch_core_version = "2.1.0"
    def mockito_version = "4.6.1"
    def okhttp_version = '5.0.0-alpha.8'
    def timber_version = "5.0.1"
    def coil_version = "2.1.0"
    def lifecycle_version = "2.5.0-rc02"
    def mpandroidchart_version = "v3.1.0"
    def qrcode_version = "3.4.1"
    def journeyapps_version = "4.3.0"
    def work_version = "2.7.1"
    def hiltwork_version = "1.0.0"

    implementation "androidx.core:core-ktx:1.8.0"
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.material:material-icons-extended:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation "androidx.compose.runtime:runtime-livedata:$compose_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.activity:activity-compose:1.5.0"
    implementation "androidx.preference:preference-ktx:1.2.0"

    // Navbar
    implementation "androidx.navigation:navigation-compose:2.5.0"

    // QR Code
    implementation "com.google.zxing:core:$qrcode_version"
    implementation "com.journeyapps:zxing-android-embedded:$journeyapps_version"

    // hilt
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"

    // Room
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    // Kotlin Extensions and Coroutines support for Room
    implementation "androidx.room:room-ktx:$room_version"

    // Kotlin Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlin_version"

    // FuzzySearch
    implementation 'me.xdrop:fuzzywuzzy:1.4.0'

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-scalars:$retrofit_version"

    // Moshi
    implementation "com.squareup.moshi:moshi:$moshi_version"
    implementation "com.squareup.moshi:moshi-kotlin:$moshi_version"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshi_version"

    // OkHttp
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"

    // Accompanist
    implementation "com.google.accompanist:accompanist-systemuicontroller:0.17.0"

    // Timber
    implementation "com.jakewharton.timber:timber:$timber_version"

    // Coil
    implementation "io.coil-kt:coil-compose:$coil_version"

    //MPAndroidChart
    implementation "com.github.PhilJay:MPAndroidChart:$mpandroidchart_version"


    // Work Manager
    implementation "androidx.work:work-runtime-ktx:$work_version"
    implementation "androidx.work:work-gcm:$work_version"
    implementation "androidx.work:work-multiprocess:$work_version"
    // Hilt Workmanager
    implementation "androidx.hilt:hilt-work:$hiltwork_version"
    kapt "androidx.hilt:hilt-compiler:$hiltwork_version"


    // Local Unit Tests
    testImplementation 'junit:junit:4.13.2'
    testImplementation "com.google.truth:truth:1.1.3"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$kotlin_version"
    testImplementation "com.google.dagger:hilt-android-testing:$hilt_version"
    testImplementation "androidx.arch.core:core-testing:$arch_core_version"
    testImplementation "org.mockito:mockito-core:$mockito_version"
    testImplementation "com.squareup.okhttp3:mockwebserver:$okhttp_version"
    kaptTest "com.google.dagger:hilt-compiler:$hilt_version"

    // Instrumented Unit Tests
    androidTestImplementation "com.google.truth:truth:1.1.3"
    androidTestImplementation "com.google.dagger:hilt-android-testing:$hilt_version"
    androidTestImplementation "androidx.arch.core:core-testing:$arch_core_version"
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$kotlin_version"
    androidTestImplementation "androidx.test.ext:junit:1.1.3"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.4.0"
    androidTestImplementation "com.squareup.okhttp3:mockwebserver:$okhttp_version"
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    androidTestImplementation "org.mockito:mockito-android:$mockito_version"
    kaptAndroidTest "com.google.dagger:hilt-compiler:$hilt_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
}

kapt {
    correctErrorTypes = true
}

hilt {
    enableAggregatingTask = true
}
